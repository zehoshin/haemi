<!DOCTYPE html>
<html lang="en">
	<head>
		<title>haemi</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			@media all and ( max-width: 640px ) {
				video { 
					display: none;
				}
				#fixParticlePos {
					position: absolute;
					width: 75px;
					height: 75px;
					border-radius: 50%;
					background-color: red;
					bottom: 200px;
					right: 30px;
				}
			}
			body {
				margin: 0;
				overflow: hidden;
			}
			canvas {
				position: absolute;
				top: 0;
				left: 0;
				width: 100dvw;
				height: 100dvh;
			}
			video {
				position: absolute;
				z-index: 9999;
				width: 50%;
				left: 50%;
				transform: translateX(-50%);
			}
		</style>
	</head>
	<body>
		<video src="./src/0509Haemi.mp4" id="video"></video>
		<div id="fixParticlePos"></div>
		<script type="importmap">
			{
				"imports": {
					"three": "./src/jsm/three.module.js",
					"three/addons/": "./src/jsm/"
				}
				
			}
		</script>
		<script type="module" src="./src/script.js"></script>
		<script id="quadVert" type="x-shader/x-vertex">
			precision highp float;

			attribute vec3 position;

			void main() {
				gl_Position = vec4( position, 1.0 );
			}

		</script>
		<script id="throughFrag" type="x-shader/x-fragment">
			precision highp float;

			uniform vec2 resolution;
			uniform sampler2D texture;
			
			void main() {
				vec2 uv = gl_FragCoord.xy / resolution.xy;
				gl_FragColor = texture2D( texture, uv );
			}

		</script>
		<script id="positionFrag" type="x-shader/x-fragment">
			precision highp float;

			uniform vec2 resolution;
			uniform sampler2D texturePosition;
			uniform sampler2D textureDefaultPosition;
			uniform float time;
			uniform float speed;
			uniform float dieSpeed;
			uniform float radius;
			uniform float curlSize;
			uniform float attraction;
			uniform float initAnimation;
			uniform vec3 mouse3d;
			//uniform vec3 windDirection; 
			uniform float tornadoStrength;
			
			vec4 mod289(vec4 x) {
				return x - floor(x * (1.0 / 289.0)) * 289.0;
			}
		
			float mod289(float x) {
				return x - floor(x * (1.0 / 289.0)) * 289.0;
			}
		
			vec4 permute(vec4 x) {
				return mod289(((x*34.0)+1.0)*x);
			}
		
			float permute(float x) {
				return mod289(((x*34.0)+1.0)*x);
			}
		
			vec4 taylorInvSqrt(vec4 r) {
				return 1.79284291400159 - 0.85373472095314 * r;
			}
		
			float taylorInvSqrt(float r) {
				return 1.79284291400159 - 0.85373472095314 * r;
			}
		
			vec4 grad4(float j, vec4 ip) {
				const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
				vec4 p, s;
		
				p.xyz = floor(fract(vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
				p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
				s = vec4(lessThan(p, vec4(0.0)));
				p.xyz = p.xyz + (s.xyz * 2.0 - 1.0) * s.www;
		
				return p;
			}
		
			#define F4 0.309016994374947451
		
			vec4 simplexNoiseDerivatives(vec4 v) {
				const vec4 C = vec4(0.138196601125011, 0.276393202250021, 0.414589803375032, -0.447213595499958);
		
				vec4 i = floor(v + dot(v, vec4(F4)));
				vec4 x0 = v - i + dot(i, C.xxxx);
		
				vec4 i0;
				vec3 isX = step(x0.yzw, x0.xxx);
				vec3 isYZ = step(x0.zww, x0.yyz);
				i0.x = isX.x + isX.y + isX.z;
				i0.yzw = 1.0 - isX;
				i0.y += isYZ.x + isYZ.y;
				i0.zw += 1.0 - isYZ.xy;
				i0.z += isYZ.z;
				i0.w += 1.0 - isYZ.z;
		
				vec4 i3 = clamp(i0, 0.0, 1.0);
				vec4 i2 = clamp(i0 - 1.0, 0.0, 1.0);
				vec4 i1 = clamp(i0 - 2.0, 0.0, 1.0);
		
				vec4 x1 = x0 - i1 + C.xxxx;
				vec4 x2 = x0 - i2 + C.yyyy;
				vec4 x3 = x0 - i3 + C.zzzz;
				vec4 x4 = x0 + C.wwww;
		
				i = mod289(i);
				float j0 = permute(permute(permute(permute(i.w) + i.z) + i.y) + i.x);
				vec4 j1 = permute(permute(permute(permute(
					i.w + vec4(i1.w, i2.w, i3.w, 1.0))
					+ i.z + vec4(i1.z, i2.z, i3.z, 1.0))
					+ i.y + vec4(i1.y, i2.y, i3.y, 1.0))
					+ i.x + vec4(i1.x, i2.x, i3.x, 1.0));
		
				vec4 ip = vec4(1.0 / 294.0, 1.0 / 49.0, 1.0 / 7.0, 0.0);
		
				vec4 p0 = grad4(j0, ip);
				vec4 p1 = grad4(j1.x, ip);
				vec4 p2 = grad4(j1.y, ip);
				vec4 p3 = grad4(j1.z, ip);
				vec4 p4 = grad4(j1.w, ip);
		
				vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
				p0 *= norm.x;
				p1 *= norm.y;
				p2 *= norm.z;
				p3 *= norm.w;
				p4 *= taylorInvSqrt(dot(p4, p4));
		
				vec3 values0 = vec3(dot(p0, x0), dot(p1, x1), dot(p2, x2));
				vec2 values1 = vec2(dot(p3, x3), dot(p4, x4));
		
				vec3 m0 = max(0.5 - vec3(dot(x0, x0), dot(x1, x1), dot(x2, x2)), 0.0);
				vec2 m1 = max(0.5 - vec2(dot(x3, x3), dot(x4, x4)), 0.0);
		
				vec3 temp0 = -6.0 * m0 * m0 * values0;
				vec2 temp1 = -6.0 * m1 * m1 * values1;
		
				vec3 mmm0 = m0 * m0 * m0;
				vec2 mmm1 = m1 * m1 * m1;
		
				float dx = temp0[0] * x0.x + temp0[1] * x1.x + temp0[2] * x2.x + temp1[0] * x3.x + temp1[1] * x4.x + mmm0[0] * p0.x + mmm0[1] * p1.x + mmm0[2] * p2.x + mmm1[0] * p3.x + mmm1[1] * p4.x;
				float dy = temp0[0] * x0.y + temp0[1] * x1.y + temp0[2] * x2.y + temp1[0] * x3.y + temp1[1] * x4.y + mmm0[0] * p0.y + mmm0[1] * p1.y + mmm0[2] * p2.y + mmm1[0] * p3.y + mmm1[1] * p4.y;
				float dz = temp0[0] * x0.z + temp0[1] * x1.z + temp0[2] * x2.z + temp1[0] * x3.z + temp1[1] * x4.z + mmm0[0] * p0.z + mmm0[1] * p1.z + mmm0[2] * p2.z + mmm1[0] * p3.z + mmm1[1] * p4.z;
				float dw = temp0[0] * x0.w + temp0[1] * x1.w + temp0[2] * x2.w + temp1[0] * x3.w + temp1[1] * x4.w + mmm0[0] * p0.w + mmm0[1] * p1.w + mmm0[2] * p2.w + mmm1[0] * p3.w + mmm1[1] * p4.w;
		
				return vec4(dx, dy, dz, dw) * 49.0;
			}
		
			vec3 curl(in vec3 p, in float noiseTime, in float persistence) {
				vec4 xNoisePotentialDerivatives = vec4(0.0);
				vec4 yNoisePotentialDerivatives = vec4(0.0);
				vec4 zNoisePotentialDerivatives = vec4(0.0);
		
				for (int i = 0; i < 3; ++i) {
					float twoPowI = pow(2.0, float(i));
					float scale = 0.5 * twoPowI * pow(persistence, float(i));
		
					xNoisePotentialDerivatives += simplexNoiseDerivatives(vec4(p * twoPowI, noiseTime)) * scale;
					yNoisePotentialDerivatives += simplexNoiseDerivatives(vec4((p + vec3(123.4, 129845.6, -1239.1)) * twoPowI, noiseTime)) * scale;
					zNoisePotentialDerivatives += simplexNoiseDerivatives(vec4((p + vec3(-9519.0, 9051.0, -123.0)) * twoPowI, noiseTime)) * scale;
				}
		
				return vec3(
					zNoisePotentialDerivatives[1] - yNoisePotentialDerivatives[2],
					xNoisePotentialDerivatives[2] - zNoisePotentialDerivatives[0],
					yNoisePotentialDerivatives[0] - xNoisePotentialDerivatives[1]
				);
			}
		
			void main() {
				vec2 uv = gl_FragCoord.xy / resolution.xy;
		
				vec4 positionInfo = texture2D(texturePosition, uv);
				vec3 position = mix(vec3(0.0, -200.0, 0.0), positionInfo.xyz, smoothstep(0.0, 0.3, initAnimation));
				float life = positionInfo.a - dieSpeed;
		
				vec3 followPosition = mix(vec3(0.0, -(1.0 - initAnimation) * 200.0, 0.0), mouse3d, smoothstep(0.2, 0.7, initAnimation));
		
				if (life < 0.0) {
					positionInfo = texture2D(textureDefaultPosition, uv);
					position = positionInfo.xyz * (1.0 + (1.0 - initAnimation)) * 0.4 * radius;
					position += followPosition;
					life = 0.5 + fract(positionInfo.w * 21.4131 + time);
				} else {
					vec3 delta = followPosition - position;
					vec3 tangent = normalize(cross(vec3(0.0, 1.0, 0.0), delta));
					position += delta * (0.005 + life * 0.01) * attraction * (1.0 - smoothstep(50.0, 350.0, length(delta))) * speed;
					position += curl(position * curlSize, time, 0.1 + (1.0 - life) * 0.1) * speed;
					//position += windDirection * speed * 0.1; // Apply wind direction
					position += tangent * speed * tornadoStrength;
				}
		
				gl_FragColor = vec4(position, life);
			}

		</script>
		<script id="vertexShader" type="x-shader/x-vertex">
			uniform sampler2D texturePosition;
			uniform sampler2D textureRotation;
			uniform sampler2D textureScale;
			uniform sampler2D textureAnimation;
			uniform vec2 resolution;
			uniform float time;
			attribute float colorIndex;
			attribute float glowTiming; 
			varying float vColorIndex;
			varying vec3 vNormal;
			varying vec3 vPosition;
			varying float vGlowTiming;
		
			mat3 rotationMatrix(vec3 rotation) {
				float cosX = cos(rotation.x);
				float sinX = sin(rotation.x);
				float cosY = cos(rotation.y);
				float sinY = sin(rotation.y);
				float cosZ = cos(rotation.z);
				float sinZ = sin(rotation.z);
		
				mat3 rotX = mat3(
					1.0, 0.0, 0.0,
					0.0, cosX, -sinX,
					0.0, sinX, cosX
				);
		
				mat3 rotY = mat3(
					cosY, 0.0, sinY,
					0.0, 1.0, 0.0,
					-sinY, 0.0, cosY
				);
		
				mat3 rotZ = mat3(
					cosZ, -sinZ, 0.0,
					sinZ, cosZ, 0.0,
					0.0, 0.0, 1.0
				);
		
				return rotZ * rotY * rotX;
			}
		
			void main() {
				vColorIndex = colorIndex;
				vGlowTiming = glowTiming;

				vNormal = normalize(normalMatrix * normal);
		
				vec2 uv = vec2(gl_InstanceID % int(resolution.x), gl_InstanceID / int(resolution.x)) / resolution;
				vec4 positionInfo = texture2D(texturePosition, uv);
				vec3 positionOffset = positionInfo.xyz;
				vec3 rotationAngles = texture2D(textureRotation, uv).xyz;
				float baseScale = texture2D(textureScale, uv).r; 

				float life = positionInfo.a;
				float scale = baseScale * smoothstep(0.0, 0.5, life);

				vec3 timeRotation = rotationAngles + time * vec3(0.1, 0.2, 0.3);
				mat3 rotationMat = rotationMatrix(timeRotation);
				vec3 rotatedPosition = rotationMat * position * scale;
		
				vec3 transformedPosition = rotatedPosition + positionOffset;
		
				vPosition = (modelViewMatrix * vec4(transformedPosition, 1.0)).xyz;
				vec4 mvPosition = modelViewMatrix * vec4(transformedPosition, 1.0);
				gl_Position = projectionMatrix * mvPosition;
			}
		</script>
		<script id="fragmentShader" type="x-shader/x-fragment">
			uniform vec3 color1;
			uniform vec3 color2;
			uniform vec3 lightPosition;
			uniform float ambientLightIntensity;
			uniform float metalness;
			uniform float roughness;
			uniform float time;
			uniform float glowIntensity;
			uniform float opacity;
			varying float vColorIndex;
			varying vec3 vNormal;
			varying vec3 vPosition;
			varying float vGlowTiming;
			
			vec3 fresnelSchlick(float cosTheta, vec3 F0) {
				return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
			}
			
			void main() {
				vec3 lightDir = normalize(lightPosition - vPosition);
				float lightIntensity = max(dot(vNormal, lightDir), 0.0);
				vec3 color = mix(color1, color2, vColorIndex);
				
				vec3 ambient = color * ambientLightIntensity;
				vec3 F0 = mix(vec3(0.04), color, metalness);
				vec3 viewDir = normalize(-vPosition);
				vec3 halfDir = normalize(lightDir + viewDir);
				float NDF = pow(max(dot(vNormal, halfDir), 0.0), roughness);
				float G = max(dot(vNormal, lightDir) * dot(vNormal, viewDir), 0.0);
				vec3 F = fresnelSchlick(max(dot(halfDir, viewDir), 0.0), F0);
			
				vec3 kD = vec3(1.0) - F;
				kD *= 1.0 - metalness;
			
				vec3 diffuse = kD * color * lightIntensity;
				vec3 specular = F * NDF * G / (4.0 * max(dot(vNormal, lightDir), 0.0) * max(dot(vNormal, viewDir), 0.0) + 0.001);
			
				float glow = pow(1.0 - dot(vNormal, viewDir), 2.0) * glowIntensity * sin(time * 0.1 + vGlowTiming);
				vec3 glowColor = vec3(1.0, 1.0, 1.0) * glow;
			
				vec3 finalColor = ambient + diffuse + specular + glowColor;
				gl_FragColor = vec4(finalColor, opacity);
			}
		</script>

		<script id="glowVertex" type="x-shader/x-vertex">
			uniform sampler2D texturePosition;
			uniform sampler2D textureScale;
			uniform vec2 resolution;
			varying vec3 vPosition;
			varying vec3 vNormal;
		
			void main() {
				vec2 uv = vec2(gl_InstanceID % int(resolution.x), gl_InstanceID / int(resolution.x)) / resolution;
				vec4 positionInfo = texture2D(texturePosition, uv);
				vec4 scaleInfo = texture2D(textureScale, uv);
		
				vec3 scaledPosition = position * scaleInfo.r; 
				vPosition = positionInfo.xyz + scaledPosition; 
				vNormal = normal;
		
				gl_Position = projectionMatrix * modelViewMatrix * vec4(vPosition + position, 1.0);
			}
		</script>
		<script id="glowFragment" type="x-shader/x-fragment">
			uniform vec3 glowColor;
			uniform float falloff;
			uniform float glowSharpness;
			uniform float glowInternalRadius;
			uniform float opacity;
			uniform vec3 scale;
	  
			varying vec3 vPosition;
			varying vec3 vNormal;
	  
			void main()
			{
			  // Normal
			  vec3 normal = normalize(vNormal);
			  if(!gl_FrontFacing)
				  normal *= - 1.0;
			  vec3 viewDirection = normalize(cameraPosition - vPosition * scale);
			  float fresnel = dot(viewDirection, normal);
			  fresnel = pow(fresnel, glowInternalRadius + 0.1);
			  float falloff = smoothstep(0., falloff, fresnel);
			  float fakeGlow = fresnel;
			  fakeGlow += fresnel * glowSharpness;
			  fakeGlow *= falloff;
			  gl_FragColor = vec4(clamp(glowColor * fresnel, 0., 1.0), clamp(fakeGlow, 0., opacity));
	  
			  #include <tonemapping_fragment>
			  #include <colorspace_fragment>
			} 
		</script>

	</body>
</html>
